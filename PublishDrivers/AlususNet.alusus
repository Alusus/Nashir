Apm.importFile("Alusus/AlususNet");

@merge module Nashir {
    class AlususNetPublishDriver {
        @injection def driver: PublishDriver;
        def publishProjectName: String;
        def apiToken: String;

        handler this~init(projName: String, apiToken: String) {
            this.publishProjectName = projName;
            this.apiToken = apiToken;
        }

        handler this_type(): SrdRef[PublishDriver] {
            return SrdRef[AlususNetPublishDriver]().{ alloc()~init(String(), String()) };
        }

        handler this_type(pubProjName: String): SrdRef[PublishDriver] {
            return SrdRef[AlususNetPublishDriver]().{ alloc()~init(pubProjName, String()) };
        }

        handler this_type(pubProjName: String, apiTok: String): SrdRef[PublishDriver] {
            return SrdRef[AlususNetPublishDriver]().{ alloc()~init(pubProjName, apiTok) };
        }

        handler this.projectName: String {
            if this.publishProjectName != "" return this.publishProjectName
            else return config.projectName;
        }

        handler (this: PublishDriver).publish() set_ptr {
            def alususNetClient: AlususNet.Client(this.projectName, this.apiToken, verbose);
            alususNetClient.publish(config.serverPort, buildPath, String("start"));
        }
        
        handler (this: PublishDriver).restoreDbBackup() set_ptr {
            def alususNetClient: AlususNet.Client(this.projectName, this.apiToken, verbose);
            alususNetClient.uploadBackup(config.dbRestorePath);
        }
        
        handler (this: PublishDriver).downloadDbBackup() set_ptr {
            def alususNetClient: AlususNet.Client(this.projectName, this.apiToken, verbose);
            alususNetClient.downloadBackup(config.dbBackupPath);
        }
    }
}
